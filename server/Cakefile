{exec, spawn} = require 'child_process'

task 'tree', 'Show a tree of the project folder', ->
    exec 'tree -I node_modules -C', (err, stdout, stderr)->
        console.log stdout

task 'devserver', 'Run the server using nodemon', ->
    nodemon = spawn 'nodemon',
        ['-w', './', '-x', 'coffee', 'src/server.coffee'],
        cwd: __dirname
    
    nodemon.stdout.on 'data', (data)->
        console.log data.toString()

    nodemon.on 'exit', (code)->
        console.log "Nodemon exited with code #{code}"

task 'build', 'Builds the project into JavaSript (./lib)', ->
    console.log 'Building...'
    exec 'coffee -co lib src', (err, stdout, stderr)->
        if stderr
            console.error stderr
        else
            console.log stdout
            console.log 'Project built to ./lib'

task 'updatestreams', 'Update live streams', ->
    justintv = require './src/justintv'
    Stream = require './src/models/stream'

    justintv.fetchLiveStreams (streams)->
        # streams can be null
        if !streams then return

        # If not, run the update
        Stream.updateAllFromRaw streams

task 'updateloop', 'Runs updatestreams once per minute', ->
    justintv = require './src/justintv'
    Stream = require './src/models/stream'
    
    update = ->
        try
            justintv.fetchLiveStreams (streams)->
                # streams can be null
                if !streams then return

                # If not, run the update
                Stream.updateAllFromRaw streams, ->
                    console.log 'Waiting 60 seconds for the next update'.magenta
        catch error
            process.stderr.write 'An error occurred: ' + String(error) + '\n'
    
    update()

    setInterval ->
        update()
    , 60000

task 'startupdateloop', 'Runs updateloop as a daemon using forever', ->
    return
